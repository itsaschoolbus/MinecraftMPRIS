plugins {
	id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
}

def getCommitHash = {
	try {
		return ['git', 'describe', '--always', '--dirty', '--exclude', '*'].execute().text.trim()
	} catch (Exception) {
		return "unknown"
	}
}

def git_commit = getCommitHash()

version = "${mod_version}+mc${minecraft_version}-${git_commit}"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "Fabric"
		url = "https://maven.fabricmc.net"
	}
	maven {
		name = "ParchmentMC"
		url = "https://maven.parchmentmc.org"
	}
	maven {
		name = "Xander Maven"
		url = "https://maven.isxander.dev/releases"
	}
	maven {
		name = "Terraformers"
		url = "https://maven.terraformersmc.com"
	}
	mavenCentral()
}

loom {
	mods {
		"minecraftmpris" {
			sourceSet sourceSets.main
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.21.11:2025.12.20@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
	modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("COPYING") {
		rename { "${it}_${base.archivesName.get()}"}
	}
	from("COPYING.LESSER") {
		rename { "${it}_${base.archivesName.get()}"}
	}
}
